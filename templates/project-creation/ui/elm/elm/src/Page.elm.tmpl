module Page exposing (Page(..), view, viewErrors)

import Api.Request exposing (Cred)
import Browser exposing (Document)
import Debug
import Html exposing (Html, a, button, div, footer, i, img, li, main_, nav, p, section, span, text, ul)
import Html.Attributes exposing (class, classList, href, style)
import Html.Events exposing (onClick)
import Route exposing (Route)
import Session exposing (Session)
import Viewer exposing (Viewer)


{-| Determines which navbar link (if any) will be rendered as active.

Note that we don't enumerate every page here, because the navbar doesn't
have links for every page. Anything that's not part of the navbar falls
under Other.

-}
type Page
    = Other
    | Home
    | Version
    | ServicesStatus
    | Echo
    | Login


{-| Take a page's Html and frames it with a header and footer.

The caller provides the current user, so we can display in either
"signed in" (rendering username) or "signed out" mode.

isLoading is for determining whether we should show a loading spinner
in the header. (This comes up during slow page transitions.)

-}
view : Maybe Viewer -> Page -> { title : String, content : Html msg } -> Document msg
view maybeViewer page { title, content } =
    { title = title ++ " - {{ upperPascalCase .ShortName }}"
    , body = viewHeader page maybeViewer :: viewContent content :: [ viewFooter ]
    }


viewContent : Html msg -> Html msg
viewContent content =
    main_ [ class "main-container" ] [ content ]


viewHeader : Page -> Maybe Viewer -> Html msg
viewHeader page maybeViewer =
    nav [ class "navbar" ]
        [ ul [] <|
            navbarLink page Route.Home (viewIconText "home" "Home")
                :: viewMenu page maybeViewer
        ]


viewIconText : String -> String -> List (Html msg)
viewIconText ico lbl =
    [ text (lbl ++ "\u{00A0}"), i [ class ("fas fa-" ++ ico) ] [] ]


viewMenu : Page -> Maybe Viewer -> List (Html msg)
viewMenu page maybeViewer =
    let
        linkTo =
            navbarLink page
    in
    case maybeViewer of
        Just viewer ->
            [ linkTo Route.Home (viewIconText "home" "Home")
            ]

        Nothing ->
            [ linkTo Route.Version (viewIconText "code-branch" "Version")
            , linkTo Route.ServicesStatus (viewIconText "info-circle" "Services Status")
            , linkTo Route.Echo (viewIconText "comment" "Echo")

            --, linkTo Route.Login (viewIconText "comment" "Login")
            ]


viewFooter : Html msg
viewFooter =
    footer [ class "footer" ]
        [ section
            [ class "float-right" ]
            [ a [ class "logo-footer", Route.href Route.Home ] [ text "{{ upperPascalCase .ShortName }} Â©2018" ]
            ]
        ]


navbarLink : Page -> Route -> List (Html msg) -> Html msg
navbarLink page route linkContent =
    li [ classList [ ( "nav-item", True ), ( "active", isActive page route ) ] ]
        [ a [ class "nav-link", Route.href route ] linkContent ]


isActive : Page -> Route -> Bool
isActive page route =
    case ( page, route ) of
        ( Home, Route.Home ) ->
            True

        ( Version, Route.Version ) ->
            True

        ( ServicesStatus, Route.ServicesStatus ) ->
            True

        ( Echo, Route.Echo ) ->
            True

        ( Login, Route.Login ) ->
            True

        _ ->
            False


{-| Render dismissable errors. We use this all over the place!
-}
viewErrors : msg -> List String -> Html msg
viewErrors dismissErrors errors =
    if List.isEmpty errors then
        Html.text ""

    else
        div
            [ class "error-messages"
            , style "position" "fixed"
            , style "top" "0"
            , style "background" "rgb(250, 250, 250)"
            , style "padding" "20px"
            , style "border" "1px solid"
            ]
        <|
            List.map (\error -> p [] [ text error ]) errors
                ++ [ button [ onClick dismissErrors ] [ text "Ok" ] ]
